// The logger runs asynchronously. Example of using it for manual capture of data from a script
// running continuously and hook up a visibility-toggle button:
// (I put this in my main Mind Stone)

// var logger = import logger
//
// ?key=bumpRBegin // C to toggle console
//   logger.toggleVisibility()
// 
// ?key=backBegin
//   logger.log(foe)

var f = import helpers

var _log = [] // all the different logged elements.
  // only keeps the most recent 20 elements so that things don't get too crazy.
var maxHistory = 20
var active = 0
var _style = -2
var p_border // the panel with a VISIBLE border
var p // the panel that actually bounds the text
var emptyStyle = ui.AddStyle("#########")
var t // the text element of the console; I'm not certain but I think I need to remake it each time.

func init()
  p_border = ui.AddPanel()
  p_border.visible = true
  p_border.color = #red
  p_border.anchor = top_right
  p_border.dock = top_right
  p_border.y = 1
  p_border.w = 64
  p_border.h = 12
  p_border.style = _style
  p_border.clip = false

  p = ui.AddPanel()
  p_border.Add(p)
  p.visible = true
  p.y = -1
  p.w = p_border.w
  p.h = p_border.h-1
  p.style = emptyStyle
  p.clip = true

  t = setupTextComponent()
  p.Add(t)
  updateText()

func setupTextComponent()
  var _t = ui.AddText()
  _t.x = 1
  _t.y = 1
  _t.w = p.w-2 // you lose the border so the text needs to be a bit smaller than the panel
  _t.h = p.h-2
  _t.align = left
  _t.dock = top_left
  _t.anchor = top_left
  return _t

func restyle()
  p.style++
  ?p.style > 8
    p.style = -8

func log(_str)
  // given a string, add it to the array, trim the array, then re-derive the contents of the console panel text.
  // "newest at the end" wasn't formatting right at all, so I'm putting the newest stuff at the TOP.
  var str = totaltime + "> " + f.nz(_str, "null") + "" // I don't trust incoming stuff to actually be a string
  f.xInsert(_log, 0, str)
  ?_log.Count()>maxHistory // this is only safe because we only add one line at a time; otherwise I'd need a loop.
    _log.RemoveAt(maxHistory-1)
  updateText()

func updateText()
  t.text = string.Join("\n", _log)

func toggleVisibility()
  ?p.visible = true
    p.visible = false
    p_border.visible = false
  :
    p.visible = true
    p_border.visible = true